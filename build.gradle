buildscript {
    ext.kotlin_version = '1.3.21'
    ext.kotlin_argparser_version = '2.0.7'
    ext.kluent_version = '1.48'
    ext.spek_version = '2.0.0'
    ext.junit5_version = '5.4.0'
    ext.mockk_version = '1.9.1'

    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "org.jlleitschuh.gradle:ktlint-gradle:7.1.0"
        classpath 'com.vanniktech:gradle-maven-publish-plugin:0.8.0'
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.21'
    id "com.github.johnrengelman.shadow" version "4.0.4"
}

apply plugin: "org.jlleitschuh.gradle.ktlint"
apply plugin: "com.vanniktech.maven.publish"

group GROUP
version VERSION_NAME

repositories {
    mavenCentral()
    // add Spek 2 development repository
    maven { url "https://dl.bintray.com/spekframework/spek-dev" }
    jcenter()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "com.xenomachina:kotlin-argparser:$kotlin_argparser_version"

    // testing
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit5_version"
    testImplementation "org.spekframework.spek2:spek-dsl-jvm:$spek_version"
    testImplementation "org.amshove.kluent:kluent:$kluent_version"
    testImplementation "io.mockk:mockk:$mockk_version"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit5_version"
    testRuntimeOnly "org.spekframework.spek2:spek-runner-junit5:$spek_version"
}

// set JVM target to 1.8 for all compilation tasks
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

test {
    useJUnitPlatform {
        includeEngines 'spek2'
    }
}

mavenPublish {
    targets {
        uploadArchives {
            releaseRepositoryUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            snapshotRepositoryUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            repositoryUsername = repositoryUsername
            repositoryPassword = repositoryPassword
        }
    }
}